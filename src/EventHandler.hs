{-# LANGUAGE UnicodeSyntax #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE ViewPatterns #-}
{-# LANGUAGE BangPatterns #-}

-- A module that transforms some events such as key press/release to MIDI events.
-- It also stores some application state such as current base note, current channel, etc., and
-- handles changes of this state and provides API to get current state.
module EventHandler
     ( runEventHandler
     , EventHandlerContext (..)
     , EventHandlerInterface (..)
     , AppState (..)
     , EventToHandle (..)
     ) where

import Prelude hiding (lookup)
import Prelude.Unicode

import Data.IORef
import Data.HashMap.Strict hiding (filter)

import Control.Monad
import Control.Arrow
import Control.Concurrent (forkIO)
import Control.Concurrent.MVar (MVar, newEmptyMVar, putMVar, takeMVar)

import Sound.MIDI.Message.Channel
import Sound.MIDI.Message.Channel.Voice (normalVelocity)

-- local
import Utils
import Types
import Keys.Types
import Keys.Specific.EventHandler
import MIDIPlayer


data EventHandlerContext
  = EventHandlerContext
  { sendToMIDIPlayer âˆ· MIDIPlayerSender
  , eventsListener   âˆ· EventsListener
  , onNewAppState    âˆ· AppState â†’ IO ()
  }

data EventHandlerInterface
  = EventHandlerInterface
  { handleEvent âˆ· EventHandlerSender
  , getAppState âˆ· IO AppState
  }


data AppState
  = AppState
  { baseKey        âˆ· RowKey   -- A key of the keyboard which would be a set point
  , basePitch      âˆ· Pitch    -- A pitch that will be associated with `baseKey`
  , channel        âˆ· Channel  -- MIDI channel
  , velocity       âˆ· Velocity -- MIDI velocity for the tiggered notes
  , octave         âˆ· Octave   -- Current octave, it's not supposed to mean a real octave
                              -- but a shift from `basePitch` by `notesPerOctave`
                              -- relatively to the `baseOctave`.
  , baseOctave     âˆ· BaseOctave -- `octave` value that would be `basePitch` on `baseKey`
                                -- that means when `octave` equals `baseOctave` `basePitch`
                                -- wouldn't be shifted at all.
  , notesPerOctave âˆ· NotesPerOctave -- Indicates how many notes will be shifted by one `octave`,
                                    -- it does't mean anything but this shift step.
  , pitchMap       âˆ· HashMap RowKey Pitch -- Current mapping of pitches by keys,
                                          -- it changes when you shift `octave`, `basePitch`, etc.
  , storedEvents   âˆ· HashMap RowKey StoredEvent -- Mapping of currently triggered events such as
                                                -- note-on to trigger proper note-off in case
                                                -- something was shifted (`octave`, `basePitch`,
                                                -- `channel`, etc.) while some note isn't
                                                -- released yet.
  } deriving (Show, Eq)

data EventToHandle
  = KeyPress   RowKey
  | KeyRelease RowKey

  | NewBaseKey        RowKey
  | NewBasePitch      Pitch
  | NewChannel        Channel
  | NewVelocity       Velocity
  | NewOctave         Octave
  | NewBaseOctave     BaseOctave
  | NewNotesPerOctave NotesPerOctave

  | PanicEvent

  deriving (Show, Eq)


type EventHandlerBus    = MVar EventToHandle
type EventHandlerSender = EventToHandle â†’ IO ()
type EventsListener     = EventToHandle â†’ AppState â†’ IO ()


-- We need this to trigger correct note-off when channel/base-key/base-pitch/channel is changed
-- before last triggered note is released.
data StoredEvent
  = StoredNoteOn Channel Pitch Velocity
  deriving (Show, Eq)


defaultAppState âˆ· AppState
defaultAppState =

  AppState { baseKey        = baseKey'
           , basePitch      = basePitch'
           , channel        = minBound
           , velocity       = normalVelocity
           , octave         = octave'
           , baseOctave     = baseOctave'
           , notesPerOctave = notesPerOctave'
           , pitchMap       = getPitchMapping baseKey' basePitch'
                                              octave' baseOctave'
                                              notesPerOctave'
           , storedEvents   = empty
           }

  where baseKey'        = AKey
        basePitch'      = toPitch 19 -- 20th in [1..128]
        octave'         = fromBaseOctave baseOctave'
        baseOctave'     = BaseOctave $ Octave 4
        notesPerOctave' = NotesPerOctave 12


runEventHandler âˆ· EventHandlerContext â†’ IO EventHandlerInterface
runEventHandler ctx = do
  (bus         âˆ· EventHandlerBus) â† newEmptyMVar
  (appStateRef âˆ· IORef AppState)  â† newIORef defaultAppState

  let interface
        = EventHandlerInterface
        { handleEvent = putMVar bus
        , getAppState = readIORef appStateRef
        }

      handle (KeyPress k) = do
        appState â† readIORef appStateRef

        case lookup k $ pitchMap appState of

             Just p â†’ do let ch     = channel appState
                             vel    = velocity appState
                             stored = StoredNoteOn ch p vel

                         sendToMIDIPlayer ctx $ NoteOn ch p vel
                         updateState $ \s â†’ s { storedEvents = insert k stored $ storedEvents s }

             Nothing â†’ pure ()

      handle (KeyRelease k) = readIORef appStateRef >>= \appState â†’ eitherValue $ do

        case lookup k $ storedEvents appState of

             Just (StoredNoteOn ch p vel) â†’ Left $ do
               sendToMIDIPlayer ctx $ NoteOff ch p vel
               updateState $ \s â†’ s { storedEvents = k `delete` storedEvents s }

             Nothing â†’ Right ()

        pure $
          case lookup k $ pitchMap appState of
               Just x  â†’ sendToMIDIPlayer ctx $ NoteOff (channel appState) x $ velocity appState
               Nothing â†’ pure ()

      handle (NewBaseKey k)        = updateState $ \s â†’ s { baseKey        = k }
      handle (NewBasePitch p)      = updateState $ \s â†’ s { basePitch      = p }
      handle (NewChannel c)        = updateState $ \s â†’ s { channel        = c }
      handle (NewVelocity v)       = updateState $ \s â†’ s { velocity       = v }
      handle (NewOctave o)         = updateState $ \s â†’ s { octave         = o }
      handle (NewBaseOctave o)     = updateState $ \s â†’ s { baseOctave     = o }
      handle (NewNotesPerOctave n) = updateState $ \s â†’ s { notesPerOctave = n }

      handle PanicEvent = sendToMIDIPlayer ctx Panic

      updateState (updateStateMiddleware â†’ (â€¢ dupe) â†’ f) =
        atomicModifyIORef' appStateRef f >>= onNewAppState ctx

  (interface <$) $ forkIO $ catchThreadFail "Event Handler" $ forever $ do
    let notifyListener ev = do
          !s â† readIORef appStateRef
          forkIO $ catchThreadFail "Events listener notifier" $ eventsListener ctx ev s

    takeMVar bus >>= (handle &&& notifyListener) â€¢ uncurry (>>)

  where updateStateMiddleware âˆ· (AppState â†’ AppState) â†’ AppState â†’ AppState
        updateStateMiddleware f oldState =

          if baseKey        newState /= baseKey        oldState
          || basePitch      newState /= basePitch      oldState
          || octave         newState /= octave         oldState
          || baseOctave     newState /= baseOctave     oldState
          || notesPerOctave newState /= notesPerOctave oldState
             then newState { pitchMap = getPitchMapping baseKey' basePitch'
                                                        octave' baseOctave'
                                                        notesPerOctave' }
             else newState

          where newState        = f oldState
                baseKey'        = baseKey        newState
                basePitch'      = basePitch      newState
                octave'         = octave         newState
                baseOctave'     = baseOctave     newState
                notesPerOctave' = notesPerOctave newState


getPitchMapping âˆ· RowKey â†’ Pitch â†’ Octave â†’ BaseOctave â†’ NotesPerOctave â†’ HashMap RowKey Pitch
getPitchMapping baseKey' basePitch' octave' baseOctave' notesPerOctave' =
  fromList (zip l lp) `union` fromList (zip r rp)
  where (reverse â†’ l, r) = span (/= baseKey') allKeysOrder

        basePitchZ, octaveZ, baseOctaveZ, notesPerOctaveZ, minPitchZ, maxPitchZ âˆ· â„¤

        basePitchZ      = toInteger $ fromPitch basePitch'
        octaveZ         = toInteger $ fromOctave octave'
        baseOctaveZ     = toInteger $ fromOctave $ fromBaseOctave baseOctave'
        notesPerOctaveZ = toInteger $ fromNotesPerOctave $ notesPerOctave'

        minPitchZ       = toInteger $ fromPitch minBound
        maxPitchZ       = toInteger $ fromPitch maxBound

        octaveShiftZ, shiftedPitchZ âˆ· â„¤

        octaveShiftZ    = (baseOctaveZ - octaveZ) * notesPerOctaveZ
        shiftedPitchZ   = basePitchZ - octaveShiftZ

        isPitchInBound âˆ· â„¤ â†’ ð”¹
        isPitchInBound x = x â‰¥ minPitchZ âˆ§ x â‰¤ maxPitchZ

        lp, rp âˆ· [Pitch]

        lp = fmap (toPitch âˆ˜ fromInteger) $ filter isPitchInBound $ eitherValue $ do
          if shiftedPitchZ > minPitchZ then Right () else Left []
          let prev = pred shiftedPitchZ
          if prev > minPitchZ
             then Right [prev, pred prev .. minPitchZ]
             else Left [prev]

        rp = fmap (toPitch âˆ˜ fromInteger) $ filter isPitchInBound $ eitherValue $ do
          if maxPitchZ > shiftedPitchZ then Right () else Left [shiftedPitchZ]
          let next = succ shiftedPitchZ
          if maxPitchZ > next
             then Right [shiftedPitchZ .. maxPitchZ]
             else Left [shiftedPitchZ, next]


-- For extracting value from breakable monads
eitherValue âˆ· Either a a â†’ a
eitherValue (Left  x) = x
eitherValue (Right x) = x
